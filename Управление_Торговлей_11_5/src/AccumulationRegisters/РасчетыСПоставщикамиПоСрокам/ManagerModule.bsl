
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Т1 
	|	ПО Т.АналитикаУчетаПоПартнерам = Т1.КлючАналитики
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т1.Организация)
	|	И ЗначениеРазрешено(Т1.Партнер)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.17.183";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("1d87b923-d170-44ae-9550-190d1f7da828");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Комментарий = НСтр("ru = 'Перезаполняет хозяйственную операцию для документа ""Взаимозачет задолженности"" с видом операции ""Перенос долга"",""Перенос аванса"".
	|Исправляет хозяйственную операцию ""Погашение задолженности"" на ""Перенос аванса"" для движений зачета оплат периодом даты платежа.
	|Изменение операции ""Оплата самозанятому"" на операцию ""Оплата поставщику"".
	|Исправляет время проведения документов ""Бронирование""
	|'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра	= "РегистрНакопления.РасчетыСПоставщикамиПоСрокам";
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСПоставщикамиПоСрокам.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщикамиПоСрокам
	|ГДЕ
	|	РасчетыСПоставщикамиПоСрокам.ДокументРегистратор ССЫЛКА Документ.ВзаимозачетЗадолженности
	|		И ВЫРАЗИТЬ(РасчетыСПоставщикамиПоСрокам.ДокументРегистратор КАК Документ.ВзаимозачетЗадолженности).ВидОперации В (&ВидыОпераций)
	|		И РасчетыСПоставщикамиПоСрокам.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности)
	|	ИЛИ
	|		РасчетыСПоставщикамиПоСрокам.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПогашениеЗадолженностиКлиента)
	|		И НАЧАЛОПЕРИОДА(РасчетыСПоставщикамиПоСрокам.Период, ДЕНЬ) <> НАЧАЛОПЕРИОДА(РасчетыСПоставщикамиПоСрокам.ДатаВозникновения, ДЕНЬ)
	|		И РасчетыСПоставщикамиПоСрокам.Предоплата > 0 
	|		И РасчетыСПоставщикамиПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И &ЗачетОплатПоДатеПлатежа
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияРегистра.Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК ДвиженияРегистра
	|ГДЕ
	|	ДвиженияРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УдалитьОплатаСамозанятому)
	|
	// Исправить время движений документов ""Бронирование""
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Расчеты
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Бронирование КАК Бронирование
	|	ПО Расчеты.ДокументРегистратор = Бронирование.Ссылка
	|ГДЕ
	|	ГОД(Расчеты.Период) = ГОД(Бронирование.Дата)
	|	И МЕСЯЦ(Расчеты.Период) = МЕСЯЦ(Бронирование.Дата)
	|	И ДЕНЬ(Расчеты.Период) = ДЕНЬ(Бронирование.Дата)
	|	И (ЧАС(Расчеты.Период) <> ЧАС(Бронирование.Дата)
	|		ИЛИ МИНУТА(Расчеты.Период) <> МИНУТА(Бронирование.Дата)
	|		ИЛИ СЕКУНДА(Расчеты.Период) <> СЕКУНДА(Бронирование.Дата))
	|";
	Запрос.УстановитьПараметр("ВидыОпераций", РегистрыНакопления.РасчетыСКлиентами.ВидыОпераций());
	Запрос.УстановитьПараметр("ЗачетОплатПоДатеПлатежа", Константы.ЗачетОплатПоДатеПлатежа.Получить() = 1);
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам;
	ПолноеИмяРегистра = МетаданныеРегистра.ПолноеИмя();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяРегистра;
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	НастройкаХозяйственнойОперации = 
		Справочники.НастройкиХозяйственныхОпераций.НайтиПоРеквизиту("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ОплатаПоставщику);
	НеИспользуемаяНастройкаОперации = 
		Справочники.НастройкиХозяйственныхОпераций.НайтиПоРеквизиту("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.УдалитьОплатаСамозанятому);
	
	Если ОбновляемыеДанные.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Движения.ДокументРегистратор КАК Взаимозачет
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Движения
		|ГДЕ
		|	Движения.Регистратор В (&Регистраторы)
		|	И ВЫРАЗИТЬ(Движения.ДокументРегистратор КАК Документ.ВзаимозачетЗадолженности).ВидОперации В (&ВидыОперацийПоДолгу)
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Движения.ДокументРегистратор КАК Взаимозачет
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Движения
		|ГДЕ
		|	Движения.Регистратор В (&Регистраторы)
		|	И ВЫРАЗИТЬ(Движения.ДокументРегистратор КАК Документ.ВзаимозачетЗадолженности).ВидОперации В (&ВидыОперацийПоАвансу)
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Движения.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Движения
		|ГДЕ
		|	Движения.Регистратор В (&Регистраторы)
		|";
		Запрос.УстановитьПараметр("Регистраторы", ОбновляемыеДанные.ВыгрузитьКолонку("Регистратор"));
		Запрос.УстановитьПараметр("ВидыОперацийПоДолгу", РегистрыНакопления.РасчетыСКлиентами.ВидыОперацийПоДолгу());
		Запрос.УстановитьПараметр("ВидыОперацийПоАвансу", РегистрыНакопления.РасчетыСКлиентами.ВидыОперацийПоАвансу());
		Результаты = Запрос.ВыполнитьПакет();
		
		ПроблемныйРегистратор = Неопределено;
		ЗачетОплатПоДатеПлатежа = Константы.ЗачетОплатПоДатеПлатежа.Получить();
		ДанныеЗаменыОперации = РегистрыНакопления.РасчетыСКлиентами.ДанныеЗаменыОперации();
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
			ЭлементБлокировки.ИсточникДанных = ОбновляемыеДанные;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Регистратор", "Регистратор");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;

			Блокировка.Заблокировать();
			ВзаимозачетПереносДолга = Результаты[0].Выгрузить().ВыгрузитьКолонку("Взаимозачет");
			ВзаимозачетПереносАванса = Результаты[1].Выгрузить().ВыгрузитьКолонку("Взаимозачет");
			ВыборкаПоРегистратору = Результаты[2].Выбрать();
			Пока ВыборкаПоРегистратору.Следующий() Цикл
				
				ПроблемныйРегистратор = ВыборкаПоРегистратору.Регистратор;
				
				Набор = РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.СоздатьНаборЗаписей();
				Набор.Отбор.Регистратор.Установить(ВыборкаПоРегистратору.Регистратор);
				Набор.Прочитать();
				НаборТЗ = Набор.Выгрузить();
				ДвиженияДоИзменения = Набор.Выгрузить();
				КэшРеквизитов = Новый Соответствие;
				
				ЕстьИзменения = Ложь;
				Для Каждого Запись Из НаборТЗ Цикл
					#Область ЗаменаОперацииВзаимозачетЗадолженности
					Если Запись.ХозяйственнаяОперация = ДанныеЗаменыОперации.ОперацияВзаимозачет
						ИЛИ Запись.НастройкаХозяйственнойОперации = ДанныеЗаменыОперации.НастройкаВзаимозачет Тогда
						Если ВзаимозачетПереносДолга.Найти(Запись.ДокументРегистратор) <> Неопределено Тогда
							Запись.ХозяйственнаяОперация = ДанныеЗаменыОперации.ОперацияПереносДолга;
							Запись.НастройкаХозяйственнойОперации = ДанныеЗаменыОперации.НастройкаПереносДолга;
							ЕстьИзменения = Истина;
						ИначеЕсли ВзаимозачетПереносАванса.Найти(Запись.ДокументРегистратор) <> Неопределено Тогда
							Запись.ХозяйственнаяОперация = ДанныеЗаменыОперации.ОперацияПереносАванса;
							Запись.НастройкаХозяйственнойОперации = ДанныеЗаменыОперации.НастройкаПереносАванса;
							ЕстьИзменения = Истина;
						КонецЕсли;
					КонецЕсли;
					#КонецОбласти
					
					#Область ЗаменаОперацииПогашениеЗадолженностиКлиента
					Если Запись.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗадолженностиКлиента
						И НачалоДня(Запись.Период) <> НачалоДня(Запись.ДатаВозникновения)
						И Запись.Предоплата > 0 
						И Запись.ВидДвижения = ВидДвиженияНакопления.Расход
						И ЗачетОплатПоДатеПлатежа Тогда
						
						Запись.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПереносАванса;
						Запись.НастройкаХозяйственнойОперации = ДанныеЗаменыОперации.НастройкаПереносАванса;
						
						СтруктураПоиска = Новый Структура("ДокументРегистратор,ВидДвижения,Долг",
							Запись.ДокументРегистратор,Запись.ВидДвижения,Запись.Предоплата);
						СтрокиДолга = НаборТЗ.НайтиСтроки(СтруктураПоиска);
						СтрокиДолга[0].ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПереносАванса;
						СтрокиДолга[0].НастройкаХозяйственнойОперации = ДанныеЗаменыОперации.НастройкаПереносАванса;
						
						ЕстьИзменения = Истина;
					КонецЕсли;
					#КонецОбласти
					
					#Область ЗаменаОперацииУдалитьОплатаСамозанятому
					Если Запись.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.УдалитьОплатаСамозанятому Тогда
						
						ЕстьИзменения = Истина;
						Запись.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику;
						
						Если ЗначениеЗаполнено(Запись.НастройкаХозяйственнойОперации)
							И Запись.НастройкаХозяйственнойОперации = НеИспользуемаяНастройкаОперации Тогда
							Запись.НастройкаХозяйственнойОперации = НастройкаХозяйственнойОперации;
						КонецЕсли;
						
					КонецЕсли;
					#КонецОбласти
					
					#Область ИсправлениеВремениОперацииБронирования
					Если ТипЗнч(Запись.ДокументРегистратор) = Тип("ДокументСсылка.Бронирование") Тогда
						ДатаДокумента = КэшРеквизитов[Запись.ДокументРегистратор];
						Если ДатаДокумента = Неопределено Тогда
							ДатаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.ДокументРегистратор, "Дата");
							КэшРеквизитов.Вставить(Запись.ДокументРегистратор, ДатаДокумента);
						КонецЕсли;
						Если РегистрыНакопления.РасчетыСПоставщиками.ПроверитьИУстановитьВремяПериодаЗаписи(Запись, ДатаДокумента) Тогда
							ЕстьИзменения = Истина;
						КонецЕсли;
					КонецЕсли;
					#КонецОбласти
					
				КонецЦикла;
				
				ИзменилисьИтоги = Ложь;
				Если ЕстьИзменения Тогда
					Набор.Загрузить(НаборТЗ);
					Если Набор.Количество() > 0 Тогда
						ИзменилисьИтоги = РегистрыНакопления.РасчетыСКлиентами.ЕстьИзмененияИтоговНабора(Набор, ДвиженияДоИзменения);
					КонецЕсли;
				КонецЕсли;
				
				Если ЕстьИзменения И НЕ ИзменилисьИтоги Тогда
					ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
				Иначе
					ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Набор);
				КонецЕсли;
				
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			Если ЗначениеЗаполнено(ПроблемныйРегистратор) Тогда
				ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ПроблемныйРегистратор);
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Набор);
				ОбновлениеИнформационнойБазы.ЗарегистрироватьПроблемуСДанными(
					ПроблемныйРегистратор,
					РегистрыНакопления.РасчетыСКлиентами.ТекстНепредвиденнаяСитуацияПриОбновленииДокумента()
					+ Символы.ПС + РегистрыНакопления.РасчетыСКлиентами.ТекстРекомендацииДляРегистратораРасчетов());
			Иначе
				ТекстСообщения = НСтр("ru = 'Не удалось выполнить обработку данных регистра накопления ""Расчеты с поставщиками по срокам"", при обновлении по причине: %Причина%'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ЗаписьЖурналаРегистрации(
					ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
					УровеньЖурналаРегистрации.Ошибка,
					МетаданныеРегистра,
					ТекстСообщения);
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли